DEF  kuka_rsi_driver ( )
; ========================================
; Copyright 2024 FZI Forschungszentrum Informatik
;
; Redistribution and use in source and binary forms, with or without modification, are permitted
; provided that the following conditions are met:
;
;   1. Redistributions of source code must retain the above copyright notice, this list of
;      conditions and the following disclaimer.
;
;   2. Redistributions in binary form must reproduce the above copyright notice, this list of
;      conditions and the following disclaimer in the documentation and/or other materials provided
;      with the distribution.
;
;   3. Neither the name of the copyright holder nor the names of its contributors may be used to
;      endorse or promote products derived from this software without specific prior written
;      permission.
;
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS “AS IS” AND ANY EXPRESS OR
; IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND
; FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR
; CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
; DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
; DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
; WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY
; WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
; ========================================

; Declare KRL Variables
DECL INT ret;
DECL INT CONTID;

;FOLD INI
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)

; Move to start position
PTP {A1 0, A2 -90, A3 90, A4 0, A5 0, A6 0}

; Create RSI Context
ret = RSI_CREATE("ros_rsi.rsi", CONTID, TRUE)
IF (ret <> RSIOK) THEN
   HALT
ENDIF

; Start RSI
ret = RSI_ON(#ABSOLUTE)
if (ret <> RSIOK) THEN
   HALT
ENDIF

; Run RSI Correction
RSI_MOVECORR()

; Turn off RSI
ret = RSI_OFF()
if (ret <> RSIOK) THEN
   HALT
ENDIF

; Return to start position
PTP {A1 0, A2 -90, A3 90, A4 0, A5 0, A6 0}

END
